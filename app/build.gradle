apply plugin: 'com.android.application'

android {
    compileSdkVersion 'Vuzix Corporation:Vuzix M100 SDK Add-on:15'
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.moreants.care"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 684
        versionName "3.1.2"
        setProperty("archivesBaseName", "AffdexMe-$versionName-$versionCode")
    }
    buildTypes {
        debug {
            resValue 'string', 'app_name', 'MoreantsDebug'
            applicationIdSuffix ".debug"
        }
        release {
            resValue 'string', 'app_name', 'Moreants'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }
}

dependencies {
    //include the Affdex SDK
    compile 'com.affectiva.android:affdexsdk:3.1.2'
    //include project dependencies
    compile 'com.android.support:support-v4:23.1.1'
//    compile 'com.android.support:appcompat-v7:23.1.1'
}

// build a signed release apk only if the environment is configured
// with the properties that let us access the keystore.  you can put
// them into ~/.gradle/gradle.properties
if (project.hasProperty('releaseKeystoreFile')) {
    android.signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
